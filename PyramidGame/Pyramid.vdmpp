class Pyramid
types
	public CardNumber = nat
	   inv n == n <= 52;
	public DeckType = seq of nat1;
	public String = seq of char;
	public NaturalsNums = seq of nat;
	public NaturalsNumsDiff0 = seq of nat1;

instance variables
	public deck: seq of CardNumber := [];
	public static pyramidDeck: seq of seq of CardNumber := [];
	public static auxDeck: seq of CardNumber := [];
	public validDeck: set of CardNumber:= {};
	public static selectedCards: seq of NaturalsNums:= [];
	public static score: int := 0;
	public static selected: int := 0;
	public bonusPyramidDeck: bool := true;
	public bonusDeck: bool := true;
	
	
operations
-- Construtor
public Pyramid: () ==> Pyramid
	Pyramid() == (
		createDeck();
		createPyramidDeck();
		updateValidDeck();
		Utils`printStatus();
		return self;
	);

-- Faz jogada: seleção de carta para ir a jogo
public selectCard: nat1 * nat ==> ()
	selectCard(xCoord, yCoord) == (	
		-- Se carta selecionada já tinha sido selecionada, desseleciona-a
		for position = 1 to len selectedCards do (
			if selectedCards(position) = [yCoord,xCoord]
				then (deselectCard(selectedCards, position, yCoord, xCoord); Utils`printStatus(); return; )
		);
		-- Adiciona a carta à cartas selecionadas e incrementa o valor do selected
		if not selectedCard(yCoord, xCoord) then IO`print("Seleção inválida\n");

		if updateGame()
			then (
				updateValidDeck();
				if not continueGame() then (IO`print("Perdeste! O jogo terminou por falta de jogadas possíveis.\n"); return ));
				
		Utils`printStatus();
	)	
pre (yCoord = 0 and Utils`min(len auxDeck, 2) >= xCoord) or (yCoord <> 0 and len pyramidDeck(yCoord) >= xCoord);

-- Pesquisar se existem as condições necessárias para o jogo continuar (não terminado e com jogadas possíveis)
public continueGame: () ==> bool
	continueGame() == (	
		dcl sumCombination : nat := 0;
		dcl setValidDeck: set of CardNumber:= validDeck;
		
		if isFinished()
			then (IO`print("Parabéns! Ganhaste o jogo! O teu score é "); IO`print(score); return true);
		if len deck > 0
			then return true;
			
		-- Combina os elementos da lista exemplo e soma cada sublista
		for all value in set power setValidDeck do ( 
			sumCombination := SumSet(value);
			 if sumCombination = 13
				then return true;
		);
		return false;
);

 -- Cria deck inicial
public createDeck: () ==> ()
	createDeck() ==
	(
		deck := [ i | i in set {1, ..., 52}];
	)
post len deck = 52;

-- Cria a Piramide de Cartas
public createPyramidDeck: () ==> ()	
	createPyramidDeck() == (
		for counter = 0 to 6 do (
			dcl aux : NaturalsNumsDiff0 := [];
			for counter2 = 0 to counter do (
				dcl value: nat1 := getCard(deck);
					aux := [value] ^ aux;							
			);
			pyramidDeck := pyramidDeck ^ [aux];
		);
	)
pre len pyramidDeck = 0
post len pyramidDeck = 7;

-- Atualiza o conjunto de cartas que são válidas para jogar
public updateValidDeck: () ==> ()
	updateValidDeck() == (
		dcl validDeckLocal: set of CardNumber:= {};
		for i = 1 to 7 do (
			for j = 1 to i do (
				if checkValidCard(i, j)
					then validDeckLocal := {pyramidDeck(i)(j)} union validDeckLocal;
			);
		);
		for i = 1 to len auxDeck do (
			if i < 3 
				then validDeckLocal := {auxDeck(i)} union validDeckLocal;
		);
		validDeck := validDeckLocal;
	);

-- Verifica se a carta é válida para ser jogada
public checkValidCard: nat1 * nat1 ==> bool
	checkValidCard(i, j) == (
		if pyramidDeck(i)(j) = 0
			then return false
		else if i = 7
			then return true
		else if pyramidDeck(i+1)(j) = 0 and pyramidDeck(i+1)(j+1) = 0
			then return true
		else
			return false;
	);

-- Retira carta para baralho auxiliar
public getAuxCard: () ==> ()
	getAuxCard() == (	
		dcl value: nat1 := getCard(deck);
		auxDeck := [value] ^ auxDeck;
		updateValidDeck();
		if not continueGame() then (IO`print("Perdeste! O jogo terminou por falta de jogadas possíveis.\n"); return );
		-- Mostra carta
		Utils`printStatus();
	)
pre len deck > 0
post len auxDeck > len auxDeck~;

-- Seleção de carta pelo utilizador (adiciona às cartas selecionada e incrementa a pontuação)
public selectedCard: nat * nat1 ==> bool
	selectedCard(i, j) == (
		for all value in set validDeck do ( 
			if (i = 0 and auxDeck(j) = value) or (i <> 0 and pyramidDeck(i)(j) = value)
				then (selected := selected + Utils`cardToPoints(value);
							selectedCards:= [[i,j]] ^ selectedCards;					
							return true;)
		);	
		return false;
	);

-- Verifica e atualiza estado de jogo
public updateGame: () ==> bool
	updateGame() == (
		if selected = 13
			then (removeSelectedCards(); calcScore(); selectedCards := []; selected := 0; return true;)
		else if selected > 13
			then (selectedCards := []; selected := 0; return true;) 
		else return false;  
	);

-- Traduz regras em pontos
public combo: () ==> nat
	combo() == (
		if (len selectedCards > 2)
			then return ((len selectedCards - 2) * 10)
		else
			 return 0
);

-- Pontos bónus
public bonus: () ==> nat
	bonus() == (
		if (bonusPyramidDeck and (pyramidDeck(1)(1) = 0))
			then (bonusPyramidDeck := false; return 50;) 
		else if (bonusDeck and len deck = 0 and len auxDeck = 0)
			then (bonusDeck := false; return 30;) 
		else
			 return 0
);

-- Calcula score
public calcScore: () ==> ()
	calcScore() == (
		score := score + ((len selectedCards) * 10) + combo() + bonus();
	);

-- Verifica se jogo termina
public isFinished: () ==> bool
	isFinished() == (
		if not bonusPyramidDeck and not bonusDeck
			then return true
		else
			return false;
	);

-- Conversão para score
pure public static getPoints: (nat1) ==> nat1
	getPoints(num) == (
		dcl x: nat := num mod 13;
		if x = 0
			then return 13
		else
			 return x;
	);

-- Retira carta do deck inicial
public getCard: DeckType ==> nat1
	getCard(deckToGet) == (
		dcl position: nat1 := new MATH().rand(len deckToGet) + 1;
		deck := removeCard(deckToGet, position);
		return deckToGet(position);
	)
pre len deck > 0
post len deck < len deck~;

-- Remover carta de baralho	
public removeCard: DeckType * nat1 ==> DeckType
	removeCard(deckToRemove, position) == (
		return deckToRemove(1, ... , position - 1) ^ deckToRemove(position + 1, ... , len deck);
	)
pre len deckToRemove >= position;

-- Desseleciona carta
public deselectCard: seq of NaturalsNums * nat1 * nat * nat1 ==> ()
	deselectCard(deckToRemove, position, y, x) == (
		selectedCards := deckToRemove(1, ... , position - 1) ^ deckToRemove(position + 1, ... , len selectedCards);
		if y = 0 then selected := selected - Utils`cardToPoints(auxDeck(x))
		else selected := selected - Utils`cardToPoints(pyramidDeck(y)(x));
	)
post len selectedCards < len selectedCards~;

-- Remove cartas selecionadas
public removeSelectedCards: () ==> ()
	removeSelectedCards() == (
		for i = 1 to len selectedCards do (
			if selectedCards(i) = [0,1]
				then auxDeck := tl auxDeck
			else if selectedCards(i) = [0,2]
				then auxDeck := [auxDeck(1)] ^ auxDeck(3, ..., len auxDeck)
			else
				pyramidDeck(selectedCards(i)(1))(selectedCards(i)(2)) := 0;
		);
	);
		
functions
-- Sum Set from structure to single value
-- http://kurser.iha.dk/eit/tivdm1/Quick_Overview_of_VDM_Operators.pdf
public SumSet: set of nat -> nat
	SumSet(s) == (
		if s = {}
			then 0
		else let e in set s
					in
						getPoints(e) + SumSet(s\{e})
	);

end Pyramid